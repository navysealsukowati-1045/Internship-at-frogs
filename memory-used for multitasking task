#include <U8g2lib.h>
#include <Wire.h>

// OLED via U8G2 (I2C)
U8G2_SSD1306_128X64_NONAME_F_HW_I2C u8g2(U8G2_R0, /* reset=*/U8X8_PIN_NONE, /* scl=*/17, /* sda=*/18);

// LED
#define LED_PIN 37

// Stack size
#define STACK_SIZE 2048

// Task handles
TaskHandle_t task_led_handle;
TaskHandle_t task_oled_handle;

// Memory usage
size_t heap_start;
UBaseType_t stack_free_led = 0;
UBaseType_t stack_free_oled = 0;

// --- LED Task ---
void task_led(void *pv_parameters) {
  pinMode(LED_PIN, OUTPUT);
  while (1) {
    digitalWrite(LED_PIN, HIGH);
    vTaskDelay(pdMS_TO_TICKS(300));
    digitalWrite(LED_PIN, LOW);
    vTaskDelay(pdMS_TO_TICKS(300));

    // Capture stack free space
    stack_free_led = uxTaskGetStackHighWaterMark(NULL);
  }
}

// OLED Task 
void task_oled(void *pv_parameters) {
  u8g2.begin();

  while (1) {
    stack_free_oled = uxTaskGetStackHighWaterMark(NULL);
    size_t heap_free = esp_get_free_heap_size();
    size_t heap_used = heap_start - heap_free;

    u8g2.clearBuffer();
    u8g2.setFont(u8g2_font_5x8_tr);
    u8g2.setCursor(0, 10);
    u8g2.print("LED Stack Free: ");
    u8g2.print(stack_free_led * 4);
    u8g2.setCursor(0, 20);
    u8g2.print("OLED Stack Free: ");
    u8g2.print(stack_free_oled * 4);
    u8g2.setCursor(0, 30);
    u8g2.print("Heap Used: ");
    u8g2.print(heap_used);
    u8g2.setCursor(0, 40);
    u8g2.print("Heap Free: ");
    u8g2.print(heap_free);
    u8g2.sendBuffer();

    vTaskDelay(pdMS_TO_TICKS(1000));
  }
}

void setup() {
  Serial.begin(115200);
  delay(1000);

  // Save initial heap 
  heap_start = esp_get_free_heap_size();

  // LED task
  xTaskCreatePinnedToCore(
    task_led,
    "led_task",
    STACK_SIZE,
    NULL,
    1,
    &task_led_handle,
    1);

  // OLED task
  xTaskCreatePinnedToCore(
    task_oled,
    "oled_task",
    STACK_SIZE,
    NULL,
    1,
    &task_oled_handle,
    0);
}

void loop() {
  // kosong
}
